apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'com.google.gms.oss.licenses.plugin'

allOpen {
    annotation 'ss.proximityservice.testing.OpenClass'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'ss.proximityservice'
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 9
        versionName '2.2.1'
        testInstrumentationRunner 'ss.proximityservice.TestRunner'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    if (rootProject.file('keystore.properties').exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(rootProject.file('keystore.properties')))

        signingConfigs {
            config {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }

        buildTypes.release.signingConfig signingConfigs.config
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    dependencies {
        def lifecycle_version = '2.0.0'
        def support_version = '28.0.0'
        def dagger_version = '2.16'

        implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
        implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
        implementation "com.android.support:appcompat-v7:$support_version"
        implementation "com.android.support:recyclerview-v7:$support_version"
        implementation "com.android.support:support-compat:$support_version"
        implementation "com.android.support:support-v13:$support_version"
        implementation "com.google.dagger:dagger:$dagger_version"
        kapt "com.google.dagger:dagger-compiler:$dagger_version"
        implementation "com.google.dagger:dagger-android:$dagger_version"
        implementation "com.google.dagger:dagger-android-support:$dagger_version"
        kapt "com.google.dagger:dagger-android-processor:$dagger_version"
        implementation 'com.google.android.gms:play-services-oss-licenses:16.0.1'
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testImplementation 'junit:junit:4.12'
        testImplementation 'androidx.arch.core:core-testing:2.0.0'
        testImplementation 'org.mockito:mockito-core:2.19.1'
        testImplementation 'org.robolectric:robolectric:4.0-beta-2-SNAPSHOT'

        androidTestImplementation 'com.android.support.test:runner:1.0.2'
        androidTestImplementation 'com.android.support.test:rules:1.0.2'
        androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
        kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
    }
}
